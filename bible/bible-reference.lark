?start: ref_list

ref_list: bible_ref (LIST_SEP bible_ref)* LIST_SEP?

?bible_ref: (single_ref | dual_ref)

dual_ref: single_ref RANGE_SEP single_ref

single_ref: book_only_ref
          | book_num_ref
          | book_chap_verse_ref
          | num_only_ref
          | chap_verse_ref

book_only_ref: BOOK_NAME
book_num_ref: BOOK_NAME NUMBER
book_chap_verse_ref: BOOK_NAME CHAP_NUM VERSE_SEP VERSE_NUM
num_only_ref: NUMBER
chap_verse_ref: CHAP_NUM VERSE_SEP VERSE_NUM

CHAP_NUM: NUMBER
VERSE_NUM: NUMBER
NUMBER: INT

RANGE_SEP: "-"
LIST_SEP: MAJOR_LIST_SEP | MINOR_LIST_SEP
MAJOR_LIST_SEP: ";"
MINOR_LIST_SEP: ","
VERSE_SEP: ":"

BOOK_NAME: /\w(\w|\s)*[^0-9\s:;,\-]/

%import common.WS
%import common.INT
%ignore WS

